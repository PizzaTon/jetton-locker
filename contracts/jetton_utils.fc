#include "imports/stdlib.fc";
#include "./params.fc";
#include "./op_codes.fc";

cell pack_jetton_wallet_data(int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) inline {
    return begin_cell()
    .store_coins(balance)
    .store_slice(owner_address)
    .store_slice(jetton_master_address)
    .store_ref(jetton_wallet_code)
    .end_cell();
}

cell calculate_jetton_wallet_state_init(slice owner_address, slice jetton_master_address, cell jetton_wallet_code) inline {
    return begin_cell()
    .store_uint(0, 2)
    .store_dict(jetton_wallet_code)
    .store_dict(pack_jetton_wallet_data(0, owner_address, jetton_master_address, jetton_wallet_code))
    .store_uint(0, 1)
    .end_cell();
}

slice calculate_jetton_wallet_address(cell state_init) inline {
    return begin_cell().store_uint(4, 3)
    .store_int(workchain(), 8)
    .store_uint(cell_hash(state_init), 256)
    .end_cell()
    .begin_parse();
}

slice calculate_user_jetton_wallet_address(slice owner_address, slice jetton_master_address, cell jetton_wallet_code) inline {
    return calculate_jetton_wallet_address(calculate_jetton_wallet_state_init(owner_address, jetton_master_address, jetton_wallet_code));
}

(slice, cell) unpack_jetton_wallet(cell jetton_wallet) {
    slice ds = jetton_wallet.begin_parse();
    return (ds~load_msg_addr(), ds~load_ref());
}

() transfer_jetton(slice jetton_wallet_address, int query_id, slice to, int jetton_amount, int total_ton, int forward_ton, int mode) impure inline {
    cell msg = begin_cell()
    .store_uint(op::jetton_transfer(), 32)
    .store_uint(query_id, 64)
    .store_coins(jetton_amount) ;; jetton amount
    .store_slice(to) ;; destination
    .store_slice(my_address()) ;; response_address (maybe replace with from_address)
    .store_uint(0, 1)
    .store_coins(forward_ton)
    .store_uint(0, 1)
    .end_cell();

    var msg_body = begin_cell()
    .store_uint(0x10, 6) ;; nobounce
    .store_slice(jetton_wallet_address)
    .store_coins(total_ton) ;; 0.04 TON or zero
    .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_ref(msg);

    send_raw_message(msg_body.end_cell(), mode);
}